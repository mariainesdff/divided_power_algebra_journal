/-
Copyright (c) 2025 Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir, MarÃ­a InÃ©s de Frutos-FernÃ¡ndez
-/

import Mathlib.Algebra.MvPolynomial.Eval
import Mathlib.RingTheory.MvPolynomial.WeightedHomogeneous

/-! # Miscellaneous results about `GradedAlgebra` -/

noncomputable section

open Finset MvPolynomial DirectSum

section

theorem Finsupp.prod_mem_grade {Îº A R : Type*} [AddCommMonoid Îº] [DecidableEq Îº] [CommSemiring A]
    [CommSemiring R] [Algebra R A] {ğ’œ : Îº â†’ Submodule R A} [GradedAlgebra ğ’œ] {Ïƒ : Type*}
    {c : Ïƒ â†’â‚€ â„•} {f : Ïƒ â†’ A} {d : Ïƒ â†’ Îº} (hc : âˆ€ s âˆˆ c.support, f s âˆˆ ğ’œ (d s)) :
    (c.prod fun s e => f s ^ e) âˆˆ ğ’œ (c.sum fun s e => e â€¢ d s) := by
  classical
  rw [Finsupp.prod, Finsupp.sum]
  suffices âˆ€ s (hs : s âŠ† c.support), âˆ a âˆˆ s, f a ^ c a âˆˆ ğ’œ (âˆ‘ a âˆˆ s, c a â€¢ d a) by
    exact this c.support (subset_refl _)
  intro s hs
  induction s using Finset.induction_on with
  | empty => exact (SetLike.one_mem_graded ğ’œ)
  | insert a t ha ht =>
    rw [Finset.prod_insert ha, Finset.sum_insert ha]
    apply SetLike.mul_mem_graded
    Â· apply SetLike.pow_mem_graded _ (hc _ (hs (mem_insert_self a t)))
    Â· apply ht (subset_trans (subset_insert a t) hs)

end

namespace GradedAlgebra

section CommSemiring

variable {R : Type*} [CommSemiring R] {A : Type*} [CommSemiring A] [Algebra R A]

section AddCommMonoid

variable {Î¹ : Type*} [AddCommMonoid Î¹] (ğ’œ : Î¹ â†’ Submodule R A)

namespace GradeZero

theorem coe_smul (r : R) (x : ğ’œ 0) : (â†‘(r â€¢ x) : A) = r â€¢ x := rfl

@[simp] theorem coe_zero : (â†‘(0 : ğ’œ 0) : A) = 0 := rfl

variable [DecidableEq Î¹] [GradedAlgebra ğ’œ]

instance : One (ğ’œ 0) where
  one : ğ’œ 0 := âŸ¨1, SetLike.one_mem_graded ğ’œâŸ©

instance : Mul (ğ’œ 0) where
  mul := fun x y => âŸ¨x * y, by
    convert SetLike.mul_mem_graded x.2 y.2
    rw [add_zero]âŸ©

@[simp] theorem coe_mul (x y : ğ’œ 0) : (â†‘(x * y) : A) = x * y := rfl

@[simp] theorem val_mul (x y : ğ’œ 0) : (x * y).val = x.val * y.val := rfl

@[simp] theorem coe_one : (â†‘(1 : ğ’œ 0) : A) = 1 := rfl

theorem one_mem : (1 : A) âˆˆ ğ’œ 0 := SetLike.one_mem_graded ğ’œ

instance commSemiring : CommSemiring (ğ’œ 0) :=
  { (inferInstance : AddCommMonoid (ğ’œ 0)) with
    add  := (Â· + Â·)
    zero := 0
    mul := (Â· * Â·)
    one := 1
    natCast_zero := by simp only [Nat.cast_zero]
    natCast_succ := fun n â†¦ by simp only [Nat.cast_succ] -- TODO: Zulip?
    zero_mul := fun x => by ext; rw [coe_mul, coe_zero, zero_mul]
    mul_zero := fun x => by ext; rw [coe_mul, coe_zero, mul_zero]
    mul_assoc := fun x y z => by ext; simp only [coe_mul, mul_assoc]
    one_mul := fun x => by ext; rw [coe_mul, coe_one, one_mul]
    mul_one := fun x => by ext; rw [coe_mul, coe_one, mul_one]
    left_distrib := fun x y z => by ext; simp only [Submodule.coe_add, coe_mul, left_distrib]
    right_distrib := fun x y z => by ext; simp only [Submodule.coe_add, coe_mul, right_distrib]
    mul_comm := fun x y => by ext; simp only [coe_mul, mul_comm] }

instance algebra : Algebra R (ğ’œ 0) :=
  Algebra.ofModule' (fun r x => by ext; simp [SetLike.val_smul, Algebra.smul_mul_assoc, one_mul])
    (fun r x => by ext; simp only [SetLike.val_smul, Algebra.mul_smul_comm, mul_one])

end GradeZero

variable [DecidableEq Î¹] [GradedAlgebra ğ’œ]

/-- The projection from `A` to the degree `i` component `ğ’œ i`, as an `R`-linear map. -/
def proj' (i : Î¹) : A â†’â‚—[R] ğ’œ i where
  toFun a       := decompose ğ’œ a i
  map_add' a b  := by simp only [decompose_add, add_apply]
  map_smul' r a := by simp only [decompose_smul, RingHom.id_apply]; rfl

end AddCommMonoid

variable {Î¹ : Type*} [AddCommMonoid Î¹] [PartialOrder Î¹] [CanonicallyOrderedAdd Î¹]
  (ğ’œ : Î¹ â†’ Submodule R A) [DecidableEq Î¹] [GradedAlgebra ğ’œ]

/-- The projection from `A` to `ğ’œ 0`, as a `RingHom`. -/
@[simps]
def proj'_zeroRingHom : A â†’+* ğ’œ 0 where
  toFun a := proj' ğ’œ 0 a
  map_one' := by
    ext
    simp only [proj', LinearMap.coe_mk, AddHom.coe_mk,
     decompose_of_mem_same ğ’œ (GradeZero.one_mem ğ’œ), GradeZero.coe_one]
  map_zero'    := by simp only [proj', decompose_zero, LinearMap.coe_mk, AddHom.coe_mk, zero_apply]
  map_add' _ _ := by simp only [proj', decompose_add, LinearMap.coe_mk, AddHom.coe_mk, add_apply]
  map_mul' x y := by
    ext
    simp only [proj', LinearMap.coe_mk, AddHom.coe_mk, GradeZero.coe_mul,
      â† GradedRing.projZeroRingHom_apply ğ’œ, â† _root_.map_mul]

end CommSemiring

section CommRing

variable {R A Î¹ : Type*} [CommRing R] [CommRing A] [Algebra R A] [AddCommMonoid Î¹] [PartialOrder Î¹]
  [CanonicallyOrderedAdd Î¹] (ğ’œ : Î¹ â†’ Submodule R A) [DecidableEq Î¹] [GradedAlgebra ğ’œ]

namespace GradeZero

instance commRing : CommRing (ğ’œ 0) :=
{ (inferInstance : AddCommGroup (ğ’œ 0)) with
    add  := (Â· + Â·)
    zero := 0
    mul  := (Â· * Â·)
    one  := 1
    natCast_zero := by simp only [Nat.cast_zero]
    natCast_succ := fun n â†¦ by simp only [Nat.cast_succ]
    intCast_ofNat := fun n â†¦ by simp only [Int.cast_natCast]
    intCast_negSucc := fun n â†¦ by rw [â† Int.cast_negSucc]
    zero_mul := fun x => by ext; rw [coe_mul, coe_zero, zero_mul]
    mul_zero := fun x => by ext; rw [coe_mul, coe_zero, mul_zero]
    mul_assoc := fun x y z => by ext; simp only [coe_mul, mul_assoc]
    one_mul := fun x => by ext; rw [coe_mul, coe_one, one_mul]
    mul_one := fun x => by ext; rw [coe_mul, coe_one, mul_one]
    left_distrib := fun x y z => by ext; simp only [Submodule.coe_add, coe_mul, left_distrib]
    right_distrib := fun x y z => by ext; simp only [Submodule.coe_add, coe_mul, right_distrib]
    mul_comm := fun x y => by ext; simp only [coe_mul, mul_comm]
     }

end GradeZero

end CommRing

end GradedAlgebra

section GradedAlgebra

variable {R S : Type*} [CommSemiring R] {S : Type*} [CommSemiring S] [Algebra R S]

/-- An `R`-algebra map `f` between graded algebras `A` and `B` is homogeneous if for every degree
  `i`, `f(ğ’œ i) âŠ† â„¬ (Ï† i)`, where `Ï† : Î¹ â†’ Îº` is some provided map. -/
def GAlgHom.IsHomogeneous {Î¹ Îº : Type*} {A : Type*} [CommSemiring A] [Algebra R A]
    (ğ’œ : Î¹ â†’ Submodule R A) {B : Type*} [CommSemiring B] [Algebra R B] [Algebra S B]
    (â„¬ : Îº â†’ Submodule S B) (Ï† : Î¹ â†’ Îº) (f : A â†’â‚[R] B) : Prop :=
  âˆ€ i a, a âˆˆ ğ’œ i â†’ f a âˆˆ â„¬ (Ï† i)

/-- The evaluation of a weighted homogeneous polynomial at
  elements of adequate grades is homogeneous -/
theorem GAlgHom.IsHomogeneous_aeval {Ïƒ : Type*} {Î¹ Îº : Type*} [AddCommMonoid Î¹] [AddCommMonoid Îº]
    [DecidableEq Îº] (A : Type*) [CommSemiring A] [Algebra R A] (ğ’œ : Îº â†’ Submodule R A)
    [GradedAlgebra ğ’œ] {w : Ïƒ â†’ Î¹} (Ï† : Î¹ â†’+ Îº) (f : Ïƒ â†’ A) (h : âˆ€ s : Ïƒ, f s âˆˆ ğ’œ (Ï† (w s))) :
    GAlgHom.IsHomogeneous (weightedHomogeneousSubmodule R w) ğ’œ Ï† (MvPolynomial.aeval f) := by
  intro i p hp
  simp only [mem_weightedHomogeneousSubmodule, IsWeightedHomogeneous] at hp
  rw [p.as_sum, map_sum]
  apply Submodule.sum_mem
  intro c hc
  rw [aeval_monomial, â† smul_eq_mul, algebraMap_smul]
  apply Submodule.smul_mem
  convert Finsupp.prod_mem_grade fun s _ => h s
  rw [â† hp (mem_support_iff.mp hc), Finsupp.weight_apply,
    Finsupp.sum, map_sum, Finsupp.sum_of_support_subset _ subset_rfl]
  Â· exact Finset.sum_congr rfl (fun _ _ â†¦ map_nsmul _ _ _ )
  Â· exact fun _ _ â†¦ zero_smul _ _

end GradedAlgebra
